import printf, free;
import Map from "../libs/map.ptrs";
import List from "../libs/list.ptrs";
import RegExp from "../libs/regexp.ptrs";
import json_encode, json_decode from "../libs/json.ptrs";
import http_get from "../libs/http.ptrs";

var map = new Map();
function tryGet(key)
{
	printf("map[%s] = %s\n", cast<native>key, cast<native>*map[key]);
}
*map.foo = 42;
tryGet("foo");
*map.foo = 1337;
tryGet("foo");
map.remove("foo");
tryGet("foo");

*map.bar = 31.12;
*map[42] = map;
tryGet("bar");
tryGet(42);
tryGet("42");

delete map;
printf("\n");



var list = new List();
list.add("ahoi", 666, "yar har", 42, 31.12);
list.splice(0, 0, 13, 37);

foreach(val, i in list)
{
	printf("list[%d] = %s\n", i, cast<native>val);
}
printf("\n");



struct Test
{
	foo = "ahoi";
	bar = 666;
	somekey = undefined;
	nested = true;
	someFunction()
	{
		return 42;
	}
};

var instance : Test();
instance.foo = 1337;
instance.bar = 3112;
instance.somekey = "Hello World!";
instance.nested = new Test();

var str = json_encode(instance);
printf("%s\n\n", str);

function dumpMap(map, indent = 0)
{
	foreach(key, val in map)
	{
		for(var i = 0; i < indent; i++)
			printf(" ");

		if(val instanceof Map)
		{
			printf("%*s:\n", indent - 32,key);
			dumpMap(val, indent + 4);
		}
		else
		{
			printf("%*s: %s\n", indent - 32, key, cast<native>val);
		}
	}
}
dumpMap(json_decode(str));
printf("\n");



function dumpMatches(reg, str)
{
	var matches = reg.match(str);
	printf("For string %s\n", str);

	if(matches == null)
		return printf("\tNo Match\n");

	for(var i = 0; matches[i] != NULL; i++)
	{
		printf("\tmatch %d: %s\n", i, matches[i]);
	}
	free(matches);
}
var reg = new RegExp("a?(b+)x{3}", true);
dumpMatches(reg, "abbxxx");
dumpMatches(reg, "aaaaaabbbbbxx");
dumpMatches(reg, "bbaabbbbbxxx");
printf("\n");



var resp = http_get("http://m4gnus.de/");
printf("Response Code: %d\nReponse Message: %s\n", resp.code, resp.message);
dumpMap(resp.header);
printf("Content:\n%s\n", resp.content);
delete resp;
