import memcpy;

const EOF = 0xFFFFFFFF;
function iseof(c)
{
	return c == EOF;
}
function breakOnEof(c)
{
	return c == EOF ? undefined : true;
}

struct BoundFilter
{
	func;
	replaceVal;
	valPos;
	argc = 0;
	args[16];

	constructor(func, replaceVal = false, valPos = -1, args...)
	{
		this.func = func;
		this.replaceVal = replaceVal;

		this.valPos = valPos;
		argc = sizeof args;

		for(var i = 0; i < argc; i++)
			this.args[i] = args[i];

		if(valPos == argc)
			argc++;
		if(argc > 16)
			throw "Cannot bind more than 16 arguments to a function";
	}

	operator this => any
	{
		var sizedArgs = args[0 .. argc];
		if(valPos >= 0)
			sizedArgs[valPos] = undefined;

		while(true)
		{
			var val = func(...sizedArgs);
			if(typeof val == type<undefined>)
				return;
			if(yield val)
				return;
		}
	}

	operator val => this
	{
		var sizedArgs = args[0 .. argc];
		if(valPos >= 0)
			sizedArgs[valPos] = val;

		func(...sizedArgs);
		return true;
	}

	operator val => this => any
	{
		var sizedArgs = args[0 .. argc];
		if(valPos >= 0)
			sizedArgs[valPos] = val;

		var ret = func(...sizedArgs);

		if(typeof ret == type<undefined>)
			return;

		if(replaceVal)
			val = ret;
		if(ret)
			yield val;

		return true;
	}
};

struct byChar
{
	static operator val => this => any
	{
		if(typeof val == type<native>)
		{
			for(var i = 0; val[i] != 0; i++)
			{
				if(yield val[i])
					return;
			}
		}
		else
		{
			if(yield val)
				return;
		}

		return true;
	}
};

struct ByLine
{
	buff{1024};
	index = 0;

	operator val => this => any
	{
		if(val == '\n' || val == 0)
		{
			buff[index] = 0;
			index = 0;
			if(yield buff)
				return;
			return true;
		}
		else
		{
			buff[index++] = val;
			return false;

		}
	}
};
