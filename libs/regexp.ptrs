import memcpy;
import pcre_* from "libpcre.so.3";
import TypedArray from "./array.ptrs";

const PCRE_ERROR_NOMATCH = -1;

struct RegExp
{
	reg;

	constructor(pattern)
	{
		var err = NULL;
		var errOffset = 0;
		reg = pcre_compile!native(pattern, 0, &err, &errOffset, NULL);

		if(!reg)
			throw "PCRE: $err (index $errOffset)";
	}

	destructor()
	{
		pcre_free(reg);
	}

	test(str, offset = 0)
	{
		var count = pcre_exec!int(reg, NULL, str, sizeof str, offset, 0, NULL, 0);

		if(count < 0)
		{
			switch(count)
			{
				case PCRE_ERROR_NOMATCH:
					return false;
				default:
					throw "PCRE error $count";
			}
		}
		return true;
	}

	match(str, offset = 0, maxMatches = 16)
	{
		var offsets = new TypedArray(new_stack array{maxMatches * sizeof int * 2}, "int");
		var count = pcre_exec!int(reg, NULL, str, sizeof str, offset, 0, offsets.memory, sizeof offsets);

		if(count < 0)
		{
			switch(count)
			{
				case PCRE_ERROR_NOMATCH:
					return null;
				default:
					throw "PCRE error $count";
			}
		}

		var matches = new array[count];
		for(var i = 0; i < count; i++)
		{
			matches[i] = str[offsets[i * 2] .. offsets[i * 2 + 1]];
		}

		delete offsets;
		return matches;
	}
};
